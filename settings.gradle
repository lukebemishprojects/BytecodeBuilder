pluginManagement {
    repositories {
        gradlePluginPortal()
        maven {
            url "https://maven.lukebemish.dev/releases/"
        }
    }
}

buildscript {
    dependencyLocking {
        lockMode = LockMode.STRICT
        lockAllConfigurations()
        lockFile.set file("locks/settings-buildscript-gradle.lockfile")
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '+'
    id 'dev.lukebemish.managedversioning' version '+'
    id 'dev.lukebemish.conventions' version '+'
}

gradle.lifecycle.beforeProject {p ->
    p.buildscript{
        dependencyLocking {
            lockMode = LockMode.STRICT
            lockAllConfigurations()
            var cleanProjectName = (p.path + ":").replace(':', ' ').trim().replace(' ', '-')
            lockFile.set file("locks/${cleanProjectName}buildscript-gradle.lockfile")
        }
    }
}

dependencyResolutionManagement {
    repositories {
        mavenCentral()
    }

    repositoriesMode = RepositoriesMode.FAIL_ON_PROJECT_REPOS
}

def rootProjectName = 'bytecodebuilder'
rootProject.name = rootProjectName

managedVersioning {
    versionFile.set file('version.properties')
    versionPRs()
    versionSnapshots()

    gitHubActions {
        snapshot {
            prettyName.set 'Snapshot'
            workflowDispatch.set(true)
            onBranches.add 'main'
            gradleJob {
                buildCache()
                cacheReadOnly.set false
                javaVersion.set '21'
                name.set 'build'
                gradlew 'Build', 'build'
                gradlew 'Publish', 'publish'
                mavenSnapshot('github')
            }
        }
        release {
            prettyName.set 'Release'
            workflowDispatch.set(true)
            gradleJob {
                buildCache()
                javaVersion.set '21'
                name.set 'build'
                step {
                    setupGitUser()
                }
                readOnly.set false
                gradlew 'Tag Release', 'tagRelease'
                gradlew 'Build', 'build'
                step {
                    run.set 'git push && git push --tags'
                }
                recordVersion 'Record Version', 'version'
                dependencySubmission()
            }
            gradleJob {
                buildCache()
                javaVersion.set '21'
                name.set 'publish'
                needs.add('build')
                tag.set('${{needs.build.outputs.version}}')
                gradlew 'Publish', 'publish'
                sign()
                mavenCentral()
                mavenStaging('github')
            }
        }
        build_pr {
            prettyName.set 'Build PR'
            pullRequest.set(true)
            gradleJob {
                javaVersion.set '21'
                name.set 'build'
                gradlew 'Build', 'build'
                gradlew 'Publish', 'publish'
                pullRequestArtifact()
            }
        }
        publish_pr {
            prettyName.set 'Publish PR'
            publishPullRequestAction(
                'github',
                "dev/lukebemish/${rootProjectName}",
                'Build PR'
            )
        }
    }

    publishing {
        mavenStaging()
        mavenCentral()
        mavenPullRequest()
        mavenSnapshot()
    }
}
